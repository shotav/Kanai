plugins {
    id "java"
    id "org.openjfx.javafxplugin" version "0.0.10"
}

group "net.pryoscode"
version "1.0.0"
sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

jar {
    archiveFileName = project.name + ".jar"
    duplicatesStrategy = "include"
    manifest {
        attributes "Implementation-Title": project.name,
                   "Implementation-Version": project.version,
                   "Main-Class": project.group + "." + project.name.toLowerCase() + ".Main"
    }
    from {
        configurations.compileClasspath.filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
 
task jpackage(type: Exec) {
    if (System.getProperty("os.name").toLowerCase().contains("win")) {
        commandLine "jpackage",
                "--input", "build/libs",
                "--main-jar", project.name + ".jar",
                "--name", project.name,
                "--app-version", project.version,
                "--vendor", "PryosCode",
                "--type", "exe",
                "--icon", "src/main/resources/img/icon.ico",
                "--win-shortcut",
                "--win-menu",
                "--win-menu-group", "PryosCode"
    } else if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        commandLine "jpackage",
                "--input", "build/libs",
                "--main-jar", project.name + ".jar",
                "--name", project.name,
                "--app-version", project.version,
                "--vendor", "PryosCode",
                "--type", "dmg",
                "--icon", "src/main/resources/img/icon.icns"
    } else {
        commandLine "jpackage",
                "--input", "build/libs",
                "--main-jar", project.name + ".jar",
                "--name", project.name,
                "--app-version", project.version,
                "--vendor", "PryosCode",
                "--type", "deb",
                "--icon", "src/main/resources/img/icon.png",
                "--linux-shortcut",
                "--linux-menu-group", "PryosCode"
    }
}

javafx {
    version = "17"
    modules = [ "javafx.controls" ]
}

dependencies {
    implementation group: "com.google.code.gson", name: "gson", version: "2.8.9"
    implementation group: "org.fxmisc.richtext", name: "richtextfx", version: "0.10.7"
}